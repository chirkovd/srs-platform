buildscript {
    ext {
        encoding = 'UTF-8'

        springBootVersion = '2.6.3'
        testContainersVersion = "1.16.3"
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    }
}

plugins {
    id 'java'
    id "nu.studer.jooq" version "7.1.1"
}

allprojects {

    group 'org.systems.dipe'
    version '1.0-SNAPSHOT'

    apply plugin: 'java'

    sourceCompatibility = JavaVersion.VERSION_16
    targetCompatibility = JavaVersion.VERSION_16

    tasks.withType(JavaCompile) {
        options.encoding = "$encoding"
    }

    apply plugin: "nu.studer.jooq"
    apply plugin: 'io.spring.dependency-management'

    repositories {
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
            mavenBom "org.testcontainers:testcontainers-bom:${testContainersVersion}"
        }
        dependencies {
            dependency 'org.apache.commons:commons-collections4:4.4'
            dependency 'org.apache.commons:commons-lang3:3.12.0'

            dependency 'org.jooq:jooq:3.16.4'
            dependency 'org.jooq:jooq-meta:3.16.4'
            dependency 'org.jooq:jooq-codegen:3.16.4'

            dependency 'jakarta.xml.bind:jakarta.xml.bind-api:3.0.1'

            dependency 'org.mapstruct:mapstruct:1.4.2.Final'
            dependency 'org.mapstruct:mapstruct-processor:1.4.2.Final'
        }
    }

    dependencies {
        implementation 'org.apache.commons:commons-collections4'
        implementation 'org.apache.commons:commons-lang3'

        testImplementation 'org.mockito:mockito-core'
        testImplementation 'org.junit.jupiter:junit-jupiter'
        testImplementation 'org.junit.jupiter:junit-jupiter-params'

        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        testCompileOnly 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'

        annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

        compileOnly 'org.mapstruct:mapstruct-processor'
        annotationProcessor 'org.mapstruct:mapstruct-processor'
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    test {
        useJUnitPlatform()
    }
}

subprojects {

    if (project.name.endsWith("-service")) {
        apply from: "${rootDir}/gradle/service.gradle"
    }

}

apply from: "${rootDir}/gradle/jooq.gradle"
